WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

identifier = @{
    (("+" | "-" | "*") ~ (!("(" | ")" | ASCII_DIGIT | WHITESPACE) ~ ANY)*)
    | (ASCII_ALPHA ~ (!("(" | ")" | WHITESPACE) ~ ANY)*)
}

bool = { ("#t" | "#f") }
int = @{ ("-" | "+")? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ !("." | ^"e") }
float = @{
    ("-" | "+")?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
string = ${ "\"" ~ str_inner ~ "\"" }
str_inner = @{ str_char* }
str_char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
symbol = { identifier }
atom = _{ bool | int | float | string | symbol }

constant = { atom }
list = { "(" ~ expression* ~ ")" }
expression = _{ constant | list }
